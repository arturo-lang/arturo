;========================================================
; Arturo
; Programming Language + Bytecode VM compiler
; (c) 2019-2023 Yanis Zafirópulos
;
; @file: system/update.art
;========================================================

; TODO(Script\update) needs complete rewrite
;  currently the updater barely works, we should re-think and re-design the whole process and functionality
;  labels: installer,open discussion,enhancement,bug,critical

; » Helper functions

inform: $[content :string] ->
	print [ color #green content]


; » Main functions

chooseAsset: $[][
	
	unsupportedOS: $[os :string][
		return 
			~"your operating system (|os|) " ++
			"doesn't support self-updates"
	]
			
	case [sys\os]
		when? [ = "macosx" ] -> return 2
		when? [ = "linux"  ] -> return 0
		else -> panic unsupportedOS sys\os

]

lookForReleases: $[][
	inform "- Checking Arturo repository..."
	data: read.json "https://api.github.com/repos/arturo-lang/nightly/releases/latest"
	inform ~"- Latest version found: |data\body|"

	return data
]

getDownloadUrl: $[ data :dictionary asset :integer][
	return data | get "assets" 
                | get asset
                | get "browser_download_url"
]


downloadArturo: $[url :string file :string][
	
	ensure.that: "Is Arturo's download URL" -> 
		prefix? url « https://github.com/arturo-lang/nightly/releases/download
	
	ensure.that: "Is the correct OS" ->
		in? sys\os lower url
		
	ensure.that: "Is the same version kind" ->
		contains? url sys\release
		
	inform "- Downloading new version..."
	download.as: file url
	
]

extractDownload: $[tempFile :string binaryPath :string][
    
    output: remove.suffix binaryPath "arturo.exe"
    args: « --extract --overwrite-dir --ungzip --verbose

	inform ~"- Extracting into |output|..."
	execute ~"tar --file |tempFile| --directory |output| |args|"
]

cleanupDownload: $[][
	inform 	"- Cleaning up..."
	execute "rm -rf art.tar.gz"
	inform 	"- Done. :)"
]

acceptance: $[][
	message: "  ** Are you sure you want to install it? [y/n]: "
	confirm: input message | lower
						   | strip

	return in? confirm ["y" "yes"]
]


main: $[][

	asset: chooseAsset
	data: lookForReleases

	if? not? acceptance -> exit

	downloadLatestRelease data asset
	extractDownload
	cleanupDownload

]

main
