esthetic?: function [n, b][
    if n=0 -> return false
    k: n % b
    l: n / b

    while [l>0][
        j: l % b
        if 1 <> abs k-j -> return false
        l: l / b
        k: j
    ]
    return true
]

HEX: "0000000000ABCDEF"

getHex: function [ds][
    map ds 'd [
        (d < 10)? -> to :string d
                  -> to :string HEX\[d]
    ]
]

findEsthetics: function [base][
    limDown: base * 4
    limUp: base * 6

    cnt: 0
    i: 1
    result: new []
    while [cnt < limUp][
        if esthetic? i base [
            cnt: cnt + 1
            if cnt >= limDown ->
                'result ++ join getHex digits.base: base i
        ]
        i: i + 1
    ]
    print ["Base" base "->" (to :string limDown)++"th" "to" (to :string limUp)++"th" "esthetic numbers:"]
    print result
    print ""
]

loop 2..16 'bs ->
    findEsthetics bs

print "Esthetic numbers between 1000 and 9999:"

loop split.every: 16 select 1000..9999 'num -> esthetic? num 10 'row [
    print map to [:string] row 'item -> pad item 4
]