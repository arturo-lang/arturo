brilliant?: function [x][
    pf: factors.prime x
    and? -> 2 = size pf
         -> equal? size digits first pf
                   size digits last pf
]

brilliants: new []
i: 2
while [100 > size brilliants][
    if brilliant? i -> 'brilliants ++ i
    i: i + 1
]

print "First 100 brilliant numbers:"
loop split.every: 10 brilliants 'row [
    print map to [:string] row 'item -> pad item 4
]
print ""

i: 4
nth: 0
order: 1
while [order =< 6] [
    if brilliant? i [
        nth: nth + 1
        if i >= 10^order [
            print ["First brilliant number >= 10 ^" order "is" i "at position" nth]
            order: order + 1
        ]
    ]

    i: i + 1
]