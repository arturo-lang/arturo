curzon?: function [n,base]->
    zero? (inc base^n) % inc base*n

first50: function [b][
    result: new []
    i: 1
    while [50 > size result][
        if curzon? i b -> 'result ++ i 
        i: i + 1
    ]
    return result
]

oneThousandth: function [b][
    cnt: 0
    i: 1
    while [cnt < 1000][
        if curzon? i b -> cnt: cnt+1
        i: i + 1
    ]
    return dec i
]

loop select 2..10 => even? 'withBase [
    print ["First 50 Curzon numbers with base" withBase]
    loop split.every: 10 first50 withBase 'row [
        print map to [:string] row 'item -> pad item 4
    ]
    print ["\n1000th Curzon with base" withBase "=" oneThousandth withBase]
    print ""
]