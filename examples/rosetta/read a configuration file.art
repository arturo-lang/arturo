parseConfig: function [f][
    lines: split.lines read f
    lines: select map lines 'line [strip replace line {/[#;].*/} ""]
                            'line [not? empty? line]
    result: #[]

    loop lines 'line [
        field: first match line {/^[A-Z]+/}
        rest: strip replace line field ""
        parts: select map split.by:"," rest => strip 'part -> not? empty? part

        val: case size parts [
            0       -> true
            1       -> first parts
            any     -> parts
        ]

        result\[lower field]: val
    ]

    return result
]

loop parseConfig relative "config.file" [k,v][
    switch block? v -> print [k "->" join.with:", " v]
                    -> print [k "->" v]
]