canExecute?: function [scri][
    scr: extract.filename scri
    if sys\release='mini [
        if in? scr [
            "runtime.NumberOutOfPermittedRange"

            "branching"
            "sorting"

            "ackermann function"
            "arbitrary-precision integers - included"
            "curzon numbers"
            "iban"
            "integer overflow"
            "integer roots"
            "jacobsthal numbers"
            "long multiplication"
            "loops - continue"
            "lucas-lehmer test"
            "modular exponentation"
            "next special primes"
            "sorting algorithms - cycle sort"
            "sorting algorithms - pancake sort"
            "sylvester's sequence"
            "trigonometric functions"
            "ultra useful primes"
            "unicode strings"
            "wieferich primes"
            "xml - input"
        ] -> return false
    ]

    if sys\release='full [
        if in? scr [
            "runtime.IntegerOperationOverflow"
            "runtime.IntegerParsingOverflow"
        ] -> return false
    ]

    return true
]

WriteMode?: contains? arg "write"
NofRuns: 1
if WriteMode? ->
    NofRuns: 3

Results: #[
    benchmarks: #[]
    total: 0
]

runSet: function [title,location][
    print "====================================================================="
    print (color #cyan.bold ~"  |title|") ++ pad "(ms) time" 66 - size title
    print "====================================================================="
    tests: sort select list relative location 'loc -> contains? loc ".res"
    success: 0

    loop tests 'test [
        script: replace test ".res" ".art"
        expected: split.lines strip read test

        if canExecute? script [
            prints pad.right ((color #magenta "\n- Running: ") ++ truncate.preserve extract.filename script 40) 70
            result: map NofRuns Ã¸ [
                benchmark.get -> execute ~{arturo "|script|"}
            ]
            result: to :integer min result
            prints pad.left to :string result 10

            Results\benchmarks\[script]: result
            Results\total: Results\total + result
        ]
    ] 

    print "\n"
]

print color #green.bold "\n  Arturo"
print color #white.bold "  Benchmarker\n"
print color #gray ~"  Release: @|sys\release|\n"

runSet "Unit Tests" "../tests/unittests"
runSet "Errors" "../tests/errors"
runSet "Examples" "../examples/rosetta"

if WriteMode? [
    outFile: "benchmarks/results/macro/" ++ (to :string now\day)++"-"++(to :string now\month)++"-"++(to :string now\year) ++ ".json"
    write.json outFile Results
]