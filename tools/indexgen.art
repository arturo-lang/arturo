
docPath: "docs/website/pages/documentation"
libDocPath: ~"|docPath|/library"
fname: function [p][ extract.filename p ]
validDocFile?: function [f][ not? contains? ['.' '_'] first fname f ]

docEntries: []
loop select list libDocPath => validDocFile? 'modl [
    moduleName: fname modl
    ;print color #green moduleName

    loop select list ~"|libDocPath|/|moduleName|" => validDocFile? 'cmd [
        ;print ["\t" cmd]
        cmdData: # cmd
        fileName: fname cmd

        attributes: #[]
        if key? cmdData 'attributes [
            loop cmdData\attributes [k,v][
                attributes\[k]: v\description
            ]
        ]

        examples: ""
        if key? cmdData 'examples [
            examples: join.with:"\n" cmdData\examples
        ]

        entry: #[
            name: cmdData\name
            desc: cmdData\description
            modl: capitalize moduleName
            attr: attributes
            exam: examples
            url: ~"library/|moduleName|/|lower fileName|/"
        ]
        ; commandName: replace fname cmd ["-" ".art"] ["?" ""]
        ;inspect entry
        'docEntries ++ entry
    ]
]

write.directory Ã¸ "docs/website/theme/resources/data"
docEntries >>.compact.json "docs/website/theme/resources/data/index.json"