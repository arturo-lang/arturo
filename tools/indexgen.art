
docPath: "docs/website/pages/documentation"
libDocPath: ~"|docPath|/library"
fname: function [p][ extract.filename p ]
validDocFile?: function [f][ not? contains? ['.' '_'] first fname f ]

docEntries: []
loop select list libDocPath => validDocFile? 'modl [
    moduleName: fname modl
    print color #green moduleName

    loop select list ~"|libDocPath|/|moduleName|" => validDocFile? 'cmd [
        cmdData: # cmd
        fileName: fname cmd

        attributes: #[]
        if key? cmdData 'attributes [
            loop cmdData\attributes [k,v][
                attributes\[k]: v\description
            ]
        ]

        entry: #[
            name: cmdData\name
            desc: cmdData\description
            modl: capitalize moduleName
            attr: attributes
            exam: cmdData\examples
            url: ~"library/|moduleName|/|fileName|/"
        ]
        ; commandName: replace fname cmd ["-" ".art"] ["?" ""]
        inspect entry
        'docEntries ++ entry
    ]
]

docEntries >>.json "docs/website/theme/resources/data/index.json"