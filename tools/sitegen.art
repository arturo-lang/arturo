#!/usr/bin/env arturo

;---------------------------------------------------------------------------------
; How this works:
;---------------------------------------------------------------------------------
; - First, run this script - to generate all necessary data & templates
; - Then, go to docs/website
; - Finally, run: webize --build --at: ../../../arturo-lang.io/
; - And upload all website files to server
;
; * Don't forget to push changes to the corresponding repo!
;---------------------------------------------------------------------------------

modules: new []
functions: #[]
relatedFuncs: #[]

rosettas: sort map select list "examples/rosetta" => [in? ".res"] 'rosetta [
    (extract.basename rosetta) -- extract.extension rosetta
]

dict: new []

loop rosettas 'ro [
    'dict ++ ro
]

write "docs/website/data/rosetta.art" as.pretty.code dict

print as.pretty.code dict

; get modules and functions
loop symbols [sym][
    print ["processing:" sym]
    inf: info.get sym

    if key? inf 'module [
        fun: inf\name
        lib: inf\module
        append 'modules lib
        if not? key? functions lib [
            functions\[lib]: []
        ]
        functions\[lib]: append functions\[lib] fun

        levD: 100
        related: new #[]
        loop symbols [sym2][
            inf2: info.get sym2

            if and? key? inf2 'module
                    inf <> inf2 [
                lib2: inf2\module
                score: 0.0
                if lib=lib2 [
                    score: score + 0.8
                ]
                if and? contains? sym "?" contains? sym2 "?" [
                    score: score + 0.5
                ]
                if and? not? contains? sym "?" not? contains? sym2 "?" [
                    score: score + 0.5
                ]
                score: score + jaro sym sym2
                score: score + 4 * jaro inf\description inf2\description

                related\[new ~"|capitalize lib2|/|sym2|"]: score
            ]
        ]
        bestMatches: reverse last.n: 4 keys sort.values related
        relatedFuncs\[sym]: bestMatches
    ]
]

inspect modules

inspect relatedFuncs

unique 'modules

loop functions [modl,lst][
    target: relative join.path @["../docs/website/pages/documentation/library" lower modl]

    loop lst 'func [
        funcInfo:  info.get func
        if? funcInfo\type = :function [
            attributes: funcInfo\attrs
            arguments: funcInfo\args
            funcInfo: remove.key funcInfo 'attrs
            funcInfo: remove.key funcInfo 'args
            examples: funcInfo\example
            funcInfo: remove.key funcInfo 'example
            funcInfo\examples: examples
            funcInfo\attributes: attributes
            funcInfo\arguments: arguments
        ]
        else [
            funcInfo\returns: @[funcInfo\type]
        ]
        funcInfo: remove.key funcInfo 'address
        funcInfo\related: relatedFuncs\[func]
        funcInfo: replace strip as.pretty.code.unwrapped funcInfo "\t" "    "

        inspect funcInfo
        
        print join.path @[target ~"|func|.art"]
        if not? exists? target ->
            write.directory target Ã¸
        write join.path @[target ~{|replace func "?" "-"|.art}] funcInfo
    ]
]
funcs: new []
loop functions [modl,lst][
    loop lst 'f [
        print ["adding:" f]
        'funcs ++ f
    ]
]

print "AFTER"

sort 'funcs
funcList: new []
symbList: new []

inspect funcs

loop funcs 'f [
    print ["processing func:" f]
    inf: info.get f 

    href: lower replace ~"|inf\module|/|f|" "?" "-"
    'funcList ++ f
    'funcList ++ href

    if key? inf 'alias [
        'symbList ++ to :string inf\alias
        'symbList ++ to :string inf\name
        'symbList ++ href
    ]
]

write "docs/website/data/libraryModules.art" as.pretty.code sort unique modules
write "docs/website/data/libraryFunctions.art" as.pretty.code funcList
write "docs/website/data/librarySymbols.art" as.pretty.code symbList

inspect funcList
