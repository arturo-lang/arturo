#!/usr/bin/env arturo
;==========================================
; Tester
;
; @file: tools/tester.art
; @author: drkameleon
;==========================================

errorMargin: 0
if 1 = size arg -> errorMargin: to :integer first arg

supersuccess: 0
superskipped: 0
supertotal: 0

canExecute?: function [scr][
    ; TODO(unittests/lib.collections) Test failing for FreeBSD builds
    ;  probable reason is `store` trying to save to a path that hasn't been 
    ;  configured yet inside our VM
    ;  labels: freebsd, unit-test

    ; TODO(unittests/quantities) Test failing for FreeBSD builds
    ;  we have to investigate why...
    ;  labels: freebsd, unit-test
    
    if sys\os = "freebsd" [
        if in? scr [
            ;"lib.collections"
            "quantities"
        ] -> return false
    ]

    if sys\release='mini [
        if in? scr [
            "runtime.NumberOutOfPermittedRange"

            "branching"
            "lib.comparison"
            "lib.collections"
            "lib.files"
            "quantities"
            "sorting"

            "Ackermann function"
            "Arbitrary-precision integers (included)"
            "Arithmetic derivative"
            "Bell numbers"
            "Benford's law"
            "Combinations and permutations"
            "Curzon numbers"
            "Fermat pseudoprimes"
            "Hamming numbers"
            "IBAN"
            "Integer overflow"
            "Integer roots"
            "Jacobsthal numbers"
            "Left factorials"
            "Long multiplication"
            "Lucas-Lehmer test"
            "Magic constant"
            "Miller-Rabin primality test"
            "Modular exponentiation"
            "Next special primes"
            "Pell's equation"
            "Penta-power prime seeds"
            "Permutations - derangements"
            "Primorial numbers"
            "Quad-power prime seeds"
            "Repunit primes"
            "Sequence of primorial primes"
            "Smallest power of 6 whose decimal expansion contains n"
            "Sort a list of object identifiers"
            "Sorting algorithms/Cycle sort"
            "Sorting algorithms/Pancake sort"
            "Special factorials"
            "Square root by hand"
            "Super-d numbers"
            "Super-Poulet numbers"
            "Sylvester's sequence"
            "Trigonometric functions"
            "Ultra useful primes"
            "Unicode strings"
            "Wieferich primes"
            "XML/Input"
        ] -> return false
    ]

    if sys\release='full [
        if in? scr [
            "runtime.IntegerOperationOverflow"
            "runtime.IntegerParsingOverflow"
        ] -> return false
    ]

    return true
]

runSet: function [title,location].export:[supersuccess,supertotal,superskipped][
    print "====================================================================="
    print (color #cyan.bold ~"  |title|") ++ pad "passed?" 65 - size title
    print "====================================================================="

    unless exists? absolute location ->
        panic.code:1 ~"Location doesn't exist: |location|"

    tests: sort select list relative location 'loc -> contains? loc ".res"
    success: 0

    ;indiTimes: []

    loop tests 'test [
        script: replace test ".res" ".art"
        expected: read test
        if title="Errors" [
            expected: replace expected {═} ""
        ]
        expected: split.lines strip expected
        friendlyName: decode.url extract.filename script
        prints pad.right ((color #magenta "\n- Running: ") ++ truncate.preserve friendlyName 40) 70
        switch canExecute? friendlyName [
            ;tim: benchmark.get [
                result: execute ~{|sys\binary| --no-color "|script|"}
                cleanResult: ""
                if title="Errors" [
                    cleanResult: split.lines strip result
                    result: replace result {═} ""
                ]
                result: split.lines strip result
                if title <> "Errors" [
                    cleanResult: result
                ]

            ;]
            ; indiTimes: indiTimes ++ #[
            ;     tst: test
            ;     tim: tim
            ; ]

            switch result=expected [

                prints color #green.bold "[   YES   ]"
                success: success + 1
                supersuccess: supersuccess + 1
            ][
                print "<-------------------"
                print join.with:"\n" cleanResult
                print "------------------->"
                prints color #red "[  ERROR  ]"
            ]
        ][
            prints color #orange.bold "[ SKIPPED ]"
            superskipped: superskipped + 1
            success: success + 1
            supersuccess: supersuccess + 1
        ]
    ]

    print "\n"
    print pad.left render.template "<||= success ||> of <||= size tests ||> tasks succeeded ◀" 69
    print ""

    ;inspect sort.by:'tim indiTimes

    ; supersuccess: supersuccess + success
    supertotal: supertotal + size tests
]

print color #green.bold "\n  Arturo"
print color #white.bold "  Unit Tester\n"
print color #gray ~"  Release: @|sys\release|\n"

supertime: in's benchmark.get [
    runSet "Unit Tests" "../tests/unittests"
    ;runSet "Errors" "../tests/errors"
    runSet "Examples" "../examples/src/rosetta"
]

if supersuccess < supertotal-errorMargin [
    panic.code: 1 "Some unit-tests failed!"
]

print ""
print color #cyan "====================================================================="

print [color #cyan "|" color #white.bold "ANALYSIS                                                         " color #cyan "|"]
print color #cyan "====================================================================="
print [color #cyan "|" "Tests executed:" supertotal "( skipped:" superskipped ")" color #cyan pad "|" 36 - (size to :string supertotal)+size to :string superskipped]
print [color #cyan "|" "Tests succeeded:" supersuccess color #cyan pad "|" 49 - size to :string supertotal]
rate: to :string .format:".1f" 100 * supersuccess//supertotal
print color #cyan "====================================================================="
switch supersuccess=supertotal -> print [color #cyan "|" "Success rate:" color #green append rate "%" color #cyan pad "|" 51 - size rate]
                               -> print [color #cyan "|" "Success rate:" append rate "%" color #cyan pad "|" 51 - size rate]
printableTime: ~"|round.to: 2 to :floating scalar supertime| s"
print [color #cyan "|" "Total time:" printableTime color #cyan pad "|" 54 - size to :string printableTime]
print color #cyan "====================================================================="

exit
