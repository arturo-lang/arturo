#!/usr/bin/env arturo -d
;==========================================
; Webize
;
; Easy-to-use & minimalist 
; static website generator
; in Arturo
;
; @file: webize.art
; @author: drkameleon
;==========================================

Webize: #[
    version: "0.2"
] 

;--------------------------
; Globals
;--------------------------

Paths: #[
    root:				path\current
    build:  			"|project|/build"
    dataPath:           "|project|/data"
    pages:				"|project|/pages"
    theme:              "|project|/theme"
    resourceFavicon: 	"|project|/theme/resources/favicon"
    resourceFonts:  	"|project|/theme/resources/fonts"
    resourceImages: 	"|project|/theme/resources/images"
    resourceData:       "|project|/theme/resources/data"
    resourceScripts:	"|project|/theme/resources/scripts"
    resourceStyles: 	"|project|/theme/resources/styles"
    backend:            "|project|/backend"
]

Settings: #[]
Config: #[]
Target: "build"
Theme: "theme"
ThemeSettings: # "theme/settings.art"
InfoPadding: 13

;--------------------------
; Private Helpers
;--------------------------

{_showLogo}: function [][
    print join @ to :block {
        color #green"        _  _" color #white.bold" __        __   _     _         "  "\n"
        color #green"      _| || |_" color #white.bold" \      / /__| |__ (_)_______ "  "\n"
        color #green"     |_  -|  _|" color #white.bold" \ /\ / / _ \ '_ \| |_  / _ \"  "\n"
        color #green"     |_  |-  _|" color #white.bold"\ V  V /  __/ |_/ | |/ /  __/"  "\n"
        color #green"       |_||_|" color #white.bold"   \_/\_/ \___|_.__/|_/___\___|"
    }
]

{_showHeader}: function [message][
    print ""
    print color #magenta "==========================================================================="
    print [pad "" InfoPadding color #magenta upper message]
    print color #magenta "==========================================================================="
]

{_showInfo}: function [msg,inf][
    switch attr "section" 
        -> print [color #green ("* " ++ msg) inf]
        -> print [color #cyan ("   - " ++ msg) inf]
]

{_showInfoLine}: function [head,tail,success][
    switch success -> print [color #green pad head InfoPadding tail]
                   -> print [color #cyan pad head InfoPadding tail]
]

{_showHelp}: function [][
    print {
        Usage:
            webize [command] <options>

        Commands:
            new <name>                Creates new project
            build                     Builds current project 

            help                      Show this help screen
            version                   Show current version
    }
]

{_toolExists?}: function [cmd][ 
    not? empty? strip execute ~{!sh command -v |cmd|} 
]

{_routerDiagram}: function [p][
    join.with:" ▪︎ " split.path p -- extract.extension p
]

{_routeDiagram}: function [p][
    join.with:"/" split.path p
]

;--------------------------
; Helpers
;--------------------------

linkTo: function [location][
    i: 0 
    parts: location 
    if block? parts -> parts: join.path parts
    parts: split.path parts
    adjust: 0
    if "_index" = last pagePath -> adjust: 1
    while [and? [i < size pagePath]
                [i < size parts]]
    [
        if pagePath\[i] <> parts\[i] -> break
        i: i + 1
    ]
    ret: ""
    switch pagePath=parts [
        ret: "#"
        ret
    ] [
        ret: join.path (repeat [".."] ((size pagePath)-i)-adjust) ++ join.path slice parts i (size parts)-1
        ret: replace encode.url replace ret "?" "-" "+" "-" 
        switch ret="" -> "."
                      -> ret
    ]
]

importPartial: function [pa][
    project: path\current
    read join.path @[~ Paths\theme pa]
]

getCanonical: function [root][
    parts: ("_index"=last pagePath) ? -> chop pagePath -> pagePath
    parts: map parts 'p -> replace encode.url replace p "?" "-" "+" "-" 
    final: root ++ (lower join.path parts) ++ "/"

    final: replace final "https://" "<HTTPS>"
    replace replace final "//" "/" "<HTTPS>" "https://"
]

permalink: function [tag][
    replace lower tag " " "-"
]

iconsPath: ./"icons"

octicon: function [icon][
    read ~"|iconsPath|/octicons/|icon|.svg"
]

phosphor: function [category, icon][
    resu: read ~"|iconsPath|/phosphor/|category|/|icon|.svg"
    replace replace resu {/<svg[^>]+>/} "" "</svg>" ""
]

postprocessAlerts: function [src][
    replace src [
        {/<blockquote>\n<p[^>]*>\[!TIP\]/}
        {/<blockquote>\n<p[^>]*>\[!NOTE\]/}
        {/<blockquote>\n<p[^>]*>\[!IMPORTANT\]/}
        {/<blockquote>\n<p[^>]*>\[!WARNING\]/}
        {/<blockquote>\n<p[^>]*>\[!CAUTION\]/}
     ]@[
        ~"<blockquote class='alert alert-tip'>\n<p><b class='alert-message'>|octicon 'lightbulb|Tip</b><br>\n"
        ~"<blockquote class='alert alert-note'>\n<p><b class='alert-message'>|octicon 'info|Note</b><br>\n"
        ~"<blockquote class='alert alert-important'>\n<p><b class='alert-message'>|octicon 'report|Important</b><br>\n"
        ~"<blockquote class='alert alert-warning'>\n<p><b class='alert-message'>|octicon 'alert|Warning</b><br>\n"
        ~"<blockquote class='alert alert-caution'>\n<p><b class='alert-message'>|octicon 'stop|Caution</b><br>\n"
     ]
]

rGfm: ./"gfm.rb"
githubMarkdown: function [src][
    src >> "tmp.md" 
    ret: execute ~{:ruby |rGfm| tmp.md:}
    delete "tmp.md"
    ret: replace ret {/class="language-[^"]+"/} ""
    ret: replace ret {<pre lang="red"><code>} {<pre class="language-arturo"><code class="language-arturo">}
    ret: replace ret {<pre lang="arturo"><code>} {<pre class="language-arturo"><code class="language-arturo">}
    ret: replace ret {<pre lang="art"><code>} {<pre class="language-arturo"><code class="language-arturo">}
    ;ret: replace ret "/>" ">"
    ret: replace ret { align="center"} { style="text-align:center"}
    ret: replace ret {/<img width="[^"]+"/} {<img}
    ret: replace ret {<a href="http} {<a rel="nofollow" target="_blank" href="http}
    return postprocessAlerts ret
]

;--------------------------
; The main library
;--------------------------

{_newProject}: function [project][
    printInfo.section "creating new project:" project
    write.directory ø to :string project 
    loop Paths [k,pa][
        subpath: ~pa
        printInfo "creating:" subpath
        write.directory ø ~pa 
    ]
    write "" ~"|project|/pages/index.md.art" 
    write "" ~"|project|/theme/default.html"
    write "" ~"|project|/theme/settings.art"
    write "" ~"|project|/config.art"
]

{_buildProject}: function [loc][
    switch exists? "config.art" [
        Config: #"config.art" 

        Data: #.data[]
        loop list "data" 'f [
            if ".art" = extract.extension f ->
                set Data (extract.basename f) -- extract.extension f do read f
        ]
        ;inspect Data

        ;-------------------------
        ; Render pages
        ;-------------------------
        call {_showHeader} ["rendering"]

        pages: select list.recursive "pages" => [ suffix? & ".art"]
        pageTree: #.data[]

        loop pages 'pg [
            current: pageTree
            p: pg -- "pages/"
            parts: (split.path p) -- "_index.art"
            loop parts 'part [
                pp: part
                if contains? pp ".art" ->
                    pp: pp -- ".art"

                if not? key? current pp [
                    set current pp #.data []
                ]
                current: get current pp
            ]
        ]
        ; inspect pageTree

        count: size pages
        loop.with: 'c pages 'page [
            sourcePath: page --"pages/"
            targetRoute: sourcePath -- extract.extension page
            targetPath: ""
            switch "_index" = extract.filename page 
                -> targetPath: replace append join.path @[loc targetRoute] ".html" "_" ""
                -> targetPath: append join.path @[loc targetRoute "index"] ".html"

            targetPath: lower replace replace targetPath "?" "-" " " "-"
            targetDir: extract.directory targetPath
            targetFile: extract.filename targetPath
            
            write.directory ø targetDir 

            call {_showInfoLine} @[~"[|c+1|/|count|]" call {_routerDiagram} @[sourcePath] false]

            templatePath: ""
            loop ThemeSettings\routes [route,template][
                if not? empty? match sourcePath route [
                    ; print ["matched:" route "for:" sourcePath]
                    templatePath: join.path @[Theme template]
                    break
                ]
            ]

            do [
                pagePath: split.path targetRoute
                ;; debug
                ; print [ "\n"
                ;     "sourcePath:" sourcePath "\n"
                ;     "targetRoute:" targetRoute "\n"
                ;     "targetPath:" targetPath "\n"
                ;     "targetDir:" targetDir "\n"
                ;     "targetFile:" targetFile "\n"
                ;     "pagePath:" pagePath "\n"
                ;     "template:" templatePath "\n"
                ; ]
                W: #.data page
                rendered: new render.template read templatePath
                if key? ThemeSettings 'postprocess [
                    loop ThemeSettings\postprocess [k,v] [
                        replace 'rendered to :regex k v
                    ]
                ]
                write rendered targetPath 
                execute ~"html-minifier --collapse-whitespace --remove-comments |targetPath| -o |targetPath|"
            ]
            
        ]
        call {_showInfoLine} @["Done." "" true]

        ;-------------------------
        ; Post-processing
        ;-------------------------
        call {_showHeader} ["finalizing"]

        write.directory ø join.path @[loc "favico"]

        inspect list "backend"
        ; loop filter list "backend" => [in? "." &] 'res [ 
        ;     folderPath: join.path @[loc "backend" extract.basename res]
        ;     call {_showInfoLine} @["create" call {_routeDiagram} @[res -- "backend/"] false]
        ;     write.directory ø join.path @[loc "backend" extract.basename res] 
        ;     loop list res 'item [
        ;         tgt: join.path @[loc item]

        ;         call {_showInfoLine} @["copy" call {_routeDiagram} @[item -- "backend/"] false]
        ;         execute ~"cp |item| |tgt|"
        ;     ]
        ; ]

        loop filter list "theme/resources" => [in? "." &] 'res [ 
            folderPath: join.path @[loc "resources" extract.basename res]
            call {_showInfoLine} @["create" call {_routeDiagram} @[res -- "theme/resources/"] false]
            write.directory ø join.path @[loc "resources" extract.basename res] 
            loop list res 'item [
                tgt: join.path @[loc item -- "theme/"]

                if contains? res "favico" [
                    tgt: join.path @[loc "favico" "extra"]
                ]
                
                when [
                    [in? extract.extension item [".scss" ".sass"]] [
                        call {_showInfoLine} @["process" call {_routeDiagram} @[item -- "theme/resources/"] false]
                        newTgt: replace tgt extract.extension tgt ".css"
                        execute ~"sass --style compressed |item| |newTgt|"
                    ]
                    [in? extract.extension item [".png"]][
                        unless contains? res "favico" [
                            call {_showInfoLine} @["compress" call {_routeDiagram} @[item -- "theme/resources/"] false]
                            newTgt: replace tgt extract.extension tgt ".webp"
                            execute ~"cwebp |item| -o |newTgt|"
                        ]

                        call {_showInfoLine} @["copy" call {_routeDiagram} @[item -- "theme/resources/"] false]
                        execute ~"cp |item| |extract.directory tgt|"
                    ]
                    [in? extract.extension item [".js"]][
                        call {_showInfoLine} @["copy" call {_routeDiagram} @[item -- "theme/resources/"] false]
                        execute ~"cp |item| |extract.directory tgt|"

                        execute ~"uglifyjs --compress -o |tgt| -- |tgt|"
                    ]
                    true [
                        call {_showInfoLine} @["copy" call {_routeDiagram} @[item -- "theme/resources/"] false]
                        execute ~"cp |item| |extract.directory tgt|"
                    ]
                ]
            ]
        ]
        call {_showInfoLine} @["Done." "" true]
        print ""
    ][
        print "cannot build: not valid project"
    ]
]

initialize: function [][
    loop ['sass 'coffee 'minify 'cwebp] [tool][
        set Settings tool (call {_toolExists?} @[tool])
    ]
]

;--------------------------
; The main entry point
;--------------------------

if standalone? [
    call {_showLogo} []
    initialize

    if empty? arg [ call {_showHelp} [], exit ]
    when [
        [key? args "new"][
            call {_newProject} @[args\new]
        ]
        [key? args "build"][
            location: join.path @[path\current Target]
            if key? args 'at [ 
                location: join.path @[path\current to :string args\at ] 
            ]
            call {_buildProject} @[location]
        ]
        true  -> print "command not recognized"
    ]
]

;==========================================
; This is the end,
; my only friend, the end...
;==========================================